# $Id: PKGBUILD 197158 2013-10-23 13:04:23Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

# NOTE: requires rebuilt with each new gcc version

pkgname=('libtool' 'libltdl')
pkgver=2.4.2
_gccver=4.8.2
pkgrel=12.1
pkgdesc="A generic library support script"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/libtool"
license=('GPL')
makedepends=("gcc=${_gccver}")
source=(ftp://ftp.gnu.org/pub/gnu/libtool/${pkgname}-${pkgver}.tar.xz{,.sig}
	'nopic.patch')
md5sums=('2ec8997e0c07249eb4cbd072417d70fe'
         '1e6ba57420c82c663c85e745d11c7eed'
	 '46443cf0e3e1b4e4891812af73385615')

prepare() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  patch -p1 <${srcdir}/nopic.patch
}

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  make check
}

package_libtool() {
  depends=('sh' "libltdl=$pkgver" 'tar' "gcc=${_gccver}")
  groups=('base-devel')
  install=libtool.install

  cd ${srcdir}/${pkgbase}-${pkgver}
  
  make DESTDIR=${pkgdir} install-binSCRIPTS install-man install-info \
                         install-data-local
  rm -r ${pkgdir}/usr/share/libtool/libltdl/
}

package_libltdl() {
  pkgdesc="A system independent dlopen wrapper for GNU libtool"

  cd ${srcdir}/${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install-libLTLIBRARIES install-includeHEADERS \
                         install-ltdlincludeHEADERS install-data-local
  rm -r ${pkgdir}/usr/share/{aclocal,libtool/config}
}
